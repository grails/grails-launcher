subprojects { project ->
    description = "A standalone library for launching any version of Grails in the same JVM with an isolated classpath"
    version = "1.0.6-SNAPSHOT"
    group = "org.grails"
    
    apply plugin: "groovy"
    apply plugin: "maven"
    apply plugin: 'signing'
    apply from: rootProject.file("gradle/publish.gradle")

    repositories {
        mavenCentral()
    }

    // Move the Groovy config into the test space so we don't have groovy
    // on our runtime classpath and as a runtime dependency
    configurations {
        compile.extendsFrom = []
        testCompile.extendsFrom groovy
    }

    dependencies {
        groovy "org.codehaus.groovy:groovy-all:2.0.5"
        testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
    }

    task sourcesJar(type: Jar) {
        classifier "sources"
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        classifier "javadoc"
        from javadoc
    }

    artifacts {
        archives sourcesJar, javadocJar
    }

    signing {
        sign configurations.archives
    }
}

allprojects {
    apply plugin: "idea"
}

apply from: "gradle/idea.gradle"

task wrapper(type: Wrapper) {
    gradleVersion = "1.2"
}