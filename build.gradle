import org.apache.ivy.util.url.CredentialsStore

allprojects {
    apply plugin: "idea"
}

idea {
  project {
    jdkName "1.6"
    ipr {
      withXml { provider ->
        def node = provider.asNode()
        node.component.find { it.'@name' == 'VcsDirectoryMappings' }?.mapping[0].'@vcs' = 'Git'
        node.append(new XmlParser().parse(file("ideaCodeStyle.xml")))
      }
    }
  }
}

subprojects { project ->
    description = "A standalone library for launching any version of Grails in the same JVM with an isolated classpath"
    
    apply plugin: "groovy"
    apply plugin: "maven"


    version = "1.0.4.BUILD-SNAPSHOT"
    group = "org.grails" 
    
    repositories {
        mavenCentral()
    }

    // Move the Groovy config into the test space so we don't have groovy
    // on our runtime classpath and as a runtime dependency
    configurations {
        compile.extendsFrom = []
        testCompile.extendsFrom groovy
    }
    
    dependencies {
        groovy "org.codehaus.groovy:groovy-all:1.7.10"
        testCompile "org.spockframework:spock-core:0.5-groovy-1.7"
    }
    
    task sourcesJar(type: Jar) {
        classifier "sources"
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        classifier "javadoc"
        from javadoc
    }

    artifacts {
        archives sourcesJar, javadocJar
    }

    project.ext.poms = [project.install.repositories.mavenInstaller.pom]
    uploadArchives {
        project.ext.deployer = repositories.mavenDeployer {
            def username = project.hasProperty("grailsRepoUsername") ? project.grailsRepoUsername : null
            def password = project.hasProperty("grailsRepoPassword") ? project.grailsRepoPassword : null

            if (username && password) {
                CredentialsStore.INSTANCE.addCredentials("Artifactory Realm", "repo.grails.org", username, password)
            }
            
            repository(url: "http://repo.grails.org/grails/libs-releases-local") {
                authentication(userName: username, password: password)
            }
            snapshotRepository(url: "http://repo.grails.org/grails/libs-snapshots-local") {
                authentication(userName: username, password: password)
            }
        }
        project.poms << project.deployer.pom
    }
    
    project.ext.pomModifications = []
    project.ext.modifyPom = { Closure modification -> project.pomModifications << modification }
    project.poms*.whenConfigured { pom ->
        project.pomModifications.each { modification ->
            pom.project {
                modification.delegate = delegate
                modification.resolveStrategy = Closure.DELEGATE_FIRST
                modification()
            }
        }
    }
    
    project.modifyPom {
        name "Grails Launcher"
        description project.description
        url "http://grails.org"
        inceptionYear "2011"
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        scm {
            url "https://github.com/grails/grails-launcher/"
        }
        developers {
            [pledbrook: "Peter Ledbrook", ldaley: "Luke Daley"].each { devId, devName ->
                developer {
                    id devId
                    name devName
                    roles {
                        role "Developer"
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.0-milestone-7"
}